// *** GOURCE GUI ***
// This project aims to create a visual interface for Google's Gource.

global GG_Done as boolean
GG_Done = 0

// Strings to hold the various paths
global GG_GourcePath as string
global GG_VCSPath as string
global GG_LogoPath as string
//GG_GourcePath = "c:\users\martin\downloads\gource-0.34.win32"
//GG_VCSPath = "c:\users\martin\documents\dbprojects\openfps"
//GG_LogoPath = "c:\users\martin\downloads\gource-0.34.win32\fpslogo.png"

// Globals for the PATH button indexes.
global GPButton as integer
global VPButton as integer
global LPButton as integer

global GourceButton as integer

// Timer and Mouse globals.
global difTime as integer
global oldTime as integer
global MSX as integer
global MSY as integer
global MCLK as integer

// Init program.
sync on : sync rate 0
randomize timer()

// Init UI.
UI_Init()
GPButton = UI_NewButton(10, 10, 16, 16, "", ACTION_SETGOURCEPATH)
VPButton = UI_NewButton(10, 30, 16, 16, "", ACTION_SETVCSPATH)
LPButton = UI_NewButton(10, 50, 16, 16, "", ACTION_SETLOGOPATH)

GourceButton = UI_NewButton(650, 330, 64, 16, "Gource!", ACTION_GOURCE)
ExitButton = UI_NewButton(83,330,64,16,"Exit", ACTION_EXIT)

// Start the MAIN loop.
oldTime = hitimer()
while GG_Done = 0
    GG_Update()
    
    GG_Draw()
    ink rgb(127,127,127),0
    box 0,0,800,17
    ink rgb(255,255,255),0
    line 0,18,800,18
    ink rgb(0,0,0),0
    text 3, 1, "GourceGUI v0.1"
    ink rgb(255,255,255),0
    text 2, 0, "GourceGUI v0.1"
    sync
endwhile

UI_Terminate()
end

// Update GourceGUI functionality.
function GG_Update()
    difTime = hitimer() - oldTime
    oldTime = hitimer()
    MSX = mousex()
    MSY = mousey()
    MCLK = mouseclick()
    UI_UpdateButtons()
endfunction

// Draw the screen.
function GG_Draw()
    cls rgb(95,95,95)
    GG_DrawPaths(75, 250)
    UI_DrawButtons()
endfunction

// Draw the PATHs box.
function GG_DrawPaths(x as integer, y as integer)
    MaxLen as integer
    MaxLen = 65
    GPath as string
    VPath as string
    LPath as string
    GPath = GG_GourcePath
    VPath = GG_VCSPath
    LPath = GG_LogoPath
    if len(GPath) > MaxLen then GPath = "..." + right$(GG_GourcePath, MaxLen-3)
    if len(VPath) > MaxLen then VPath = "..." + right$(GG_VCSPath, MaxLen-3)
    if len(LPath) > MaxLen then LPath = "..." + right$(GG_LogoPath, MaxLen-3)
    ink rgb(0,0,0),0
    box x-4,y-4,x+657,y+69
    box outline x-8,y-8,x+652,y+64,rgb(255,255,255)
    ink rgb(127,127,127),0
    box x-7,y-7,x+652,y+64
    UI_MoveButton(GPButton, x, y)
    UI_MoveButton(VPButton, x, y+20)
    UI_MoveButton(LPButton, x, y+40)
    ink rgb(0,0,0), 0
    text x+21, y+2, "GOURCE PATH: "
    text x+21, y+22, "VCS PATH   : "
    text x+21, y+42, "LOGO PATH  : "
    text x+126, y+2, GPath
    text x+126, y+22, VPath
    text x+126, y+42, LPath
    ink rgb(223,223,223), 0
    text x+20, y+1, "GOURCE PATH: "
    text x+20, y+21, "VCS PATH   : "
    text x+20, y+41, "LOGO PATH  : "
    ink rgb(255,255,0), 0
    text x+125, y+1, GPath
    text x+125, y+21, VPath
    text x+125, y+41, LPath
endfunction
