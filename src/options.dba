// GOURCE GUI
// © Copyright 2011 Martin Enderleit
//
// This file is part of GourceGUI.
//
// GourceGUI is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// GourceGUI is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with GourceGUI.  If not, see <http://www.gnu.org/licenses/>.


// Initialize the Options.
function InitOptions()
    global FLAG_MultiSampling as integer
    global FLAG_Fullscreen as integer
    global FLAG_Key as integer
    global FLAG_Loop as integer
    global FLAG_Realtime as integer
    global FLAG_NoVSync as integer
    global COLOR_Background as dword
    global COLOR_Text as dword
    SetDefaultOptions()
endfunction

// Set all Options to default values.
function SetDefaultOptions()
    FLAG_MultiSampling = 0
    FLAG_Fullscreen = 0
    FLAG_Key = 0
    FLAG_Loop = 0
    FLAG_Realtime = 0
    FLAG_NoVSync = 0
    COLOR_Background = rgb(0,0,0)
    COLOR_Text = rgb(255,255,255)
endfunction

// Parse all the Options into a string.
function ParseOptions()
    GG_Options = ""
    if FLAG_MultiSampling then GG_Options = GG_Options + "--multi-sampling "
    if FLAG_Fullscreen
        GG_Options = GG_Options + "-f "
    else
        GG_Options = GG_Options + "-w "
    endif
    if FLAG_Key then GG_Options = GG_Options + "--key "
    if FLAG_Loop then GG_Options = GG_Options + "--loop "
    if FLAG_Realtime then GG_Options = GG_Options + "--realtime "
    if FLAG_NoVSync then GG_Options = GG_Options + "--no-vsync "
    if GG_LogoPath <> "" then GG_Options = GG_Options + "--logo " + GG_LogoPath + " "
    if GG_BGPath <> ""
        GG_Options = GG_Options + "--background-image " + GG_BGPath + " "
    else
        GG_Options = GG_Options + "-b " + right$(hex$(COLOR_Background), 6) + " "
    endif
endfunction

// Save options.
function SaveOptions()
    if file exist("ggoptions.cfg") = 1 then delete file "ggoptions.cfg"
    f = find free file()
    if f = 0 then exitfunction
    open to write f, "ggoptions.cfg"
        write string f, GG_GourcePath
        write string f, GG_VCSPath
        write string f, GG_LogoPath
        write string f, GG_BGPath
        write file f, FLAG_MultiSampling
        write file f, FLAG_Fullscreen
        write file f, FLAG_Key
        write file f, FLAG_Loop
        write file f, FLAG_Realtime
        write file f, FLAG_NoVSync
        write long f, COLOR_Background
        write long f, COLOR_Text
    close file f
endfunction

function LoadOptions()
    Success as boolean
    Success = 0
    if file exist("ggoptions.cfg") = 0 then exitfunction Success
    f = find free file()
    if f = 0 then exitfunction Success
    open to read f, "ggoptions.cfg"
        read string f, GG_GourcePath
        read string f, GG_VCSPath
        read string f, GG_LogoPath
        read string f, GG_BGPath
        read file f, FLAG_MultiSampling
        read file f, FLAG_Fullscreen
        read file f, FLAG_Key
        read file f, FLAG_Loop
        read file f, FLAG_Realtime
        read file f, FLAG_NoVSync
        read long f, COLOR_Background
        read long f, COLOR_Text
    close file f
    Success = 1
endfunction Success
