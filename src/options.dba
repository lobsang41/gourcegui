// GOURCE GUI
// © Copyright 2011 Martin Enderleit
//
// This file is part of GourceGUI.
//
// GourceGUI is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// GourceGUI is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with GourceGUI.  If not, see <http://www.gnu.org/licenses/>.


// HIDE flags.
#constant HIDE_NONE         0
#constant HIDE_BLOOM        1
#constant HIDE_DATE         2
#constant HIDE_DIRNAMES     4
#constant HIDE_FILES        8
#constant HIDE_FILENAMES    16
#constant HIDE_MOUSE        32
#constant HIDE_PROGRESS     64
#constant HIDE_ROOT         128
#constant HIDE_TREE         256
#constant HIDE_USERS        512
#constant HIDE_USERNAMES    1024

// Initialize the Options.
function InitOptions()
    global OPTION_SecsPerDay as float
    global OPTION_AutoSkipSecs as float
    global OPTION_FileIdleTime as float
    global FLAG_MultiSampling as integer
    global FLAG_Fullscreen as integer
    global FLAG_Key as integer
    global FLAG_Loop as integer
    global FLAG_Realtime as integer
    global FLAG_NoVSync as integer
    global FLAG_Hide as dword
    global COLOR_Background as dword
    global COLOR_Font as dword
    SetDefaultOptions()
endfunction

// Set all Options to default values.
function SetDefaultOptions()
    OPTION_SecsPerDay = 10.0
    OPTION_AutoSkipSecs = 3.0
    OPTION_FileIdleTime = 5.0
    FLAG_MultiSampling = 0
    FLAG_Fullscreen = 0
    FLAG_Key = 0
    FLAG_Loop = 0
    FLAG_Realtime = 0
    FLAG_NoVSync = 0
    FLAG_Hide = HIDE_NONE
    COLOR_Background = rgb(0,0,0)
    COLOR_Font = rgb(255,255,255)
endfunction

// Parse all the Options into a string.
function ParseOptions()
    GG_Options = ""
    if FLAG_MultiSampling then GG_Options = GG_Options + "--multi-sampling "
    if FLAG_Fullscreen
        GG_Options = GG_Options + "-f "
    else
        GG_Options = GG_Options + "-w "
    endif
    if FLAG_Key then GG_Options = GG_Options + "--key "
    if FLAG_Loop then GG_Options = GG_Options + "--loop "
    if FLAG_Realtime
        GG_Options = GG_Options + "--realtime "
    else
        GG_Options = GG_Options + "-s " + str$(OPTION_SecsPerDay) + " "
        GG_Options = GG_Options + "-a " + str$(OPTION_AutoSkipSecs) + " "
    endif
    GG_Options = GG_Options + "-i " + str$(OPTION_FileIdleTime) + " "
    if FLAG_NoVSync then GG_Options = GG_Options + "--no-vsync "
    if GG_LogoPath <> "" then GG_Options = GG_Options + "--logo " + GG_LogoPath + " "
    if GG_BGPath <> ""
        GG_Options = GG_Options + "--background-image " + GG_BGPath + " "
    else
        GG_Options = GG_Options + "-b " + right$(hex$(COLOR_Background), 6) + " "
    endif
    GG_Options = GG_Options + "--font-colour " + right$(hex$(COLOR_Font), 6) + " "
    if FLAG_Hide <> 0
        GG_Options = GG_Options + "--hide "
        if (FLAG_Hide && HIDE_BLOOM) then GG_Options = GG_Options + "bloom,"
        if (FLAG_Hide && HIDE_DATE) then GG_Options = GG_Options + "date,"
        if (FLAG_Hide && HIDE_DIRNAMES) then GG_Options = GG_Options + "dirnames,"
        if (FLAG_Hide && HIDE_FILES) then GG_Options = GG_Options + "files,"
        if (FLAG_Hide && HIDE_FILENAMES) then GG_Options = GG_Options + "filenames,"
        if (FLAG_Hide && HIDE_MOUSE) then GG_Options = GG_Options + "mouse,"
        if (FLAG_Hide && HIDE_PROGRESS) then GG_Options = GG_Options + "progress,"
        if (FLAG_Hide && HIDE_ROOT) then GG_Options = GG_Options + "root,"
        if (FLAG_Hide && HIDE_TREE) then GG_Options = GG_Options + "tree,"
        if (FLAG_Hide && HIDE_USERS) then GG_Options = GG_Options + "users,"
        if (FLAG_Hide && HIDE_USERNAMES) then GG_Options = GG_Options + "usernames"
        if right$(GG_Options,1) = "," then GG_Options = left$(GG_Options, len(GG_Options)-1)
        GG_Options = GG_Options + " "
    endif
endfunction

// Save options.
function SaveOptions()
    temp$ = get dir$()  // Save current path
    set dir GG_Path     // Make sure options are saved in same folder as GourceGUI.exe
    f = find free file()
    if f = 0 then exitfunction
    if file exist("ggoptions.cfg") = 1 then delete file "ggoptions.cfg"
    open to write f, "ggoptions.cfg"
        write string f, GG_GourcePath
        write string f, GG_VCSPath
        write string f, GG_LogoPath
        write string f, GG_BGPath
        write float f, OPTION_SecsPerDay
        write float f, OPTION_AutoSkipSecs
        write float f, OPTION_FileIdleTime
        write file f, FLAG_MultiSampling
        write file f, FLAG_Fullscreen
        write file f, FLAG_Key
        write file f, FLAG_Loop
        write file f, FLAG_Realtime
        write file f, FLAG_NoVSync
        write long f, FLAG_Hide
        write long f, COLOR_Background
        write long f, COLOR_Font
    close file f
    set dir temp$       // Restore current path
endfunction

function LoadOptions()
    Success as boolean
    Success = 0
    temp$ = get dir$()
    set dir GG_Path
    if file exist("ggoptions.cfg") = 0 then exitfunction Success
    f = find free file()
    if f = 0 then exitfunction Success
    open to read f, "ggoptions.cfg"
        read string f, GG_GourcePath
        read string f, GG_VCSPath
        read string f, GG_LogoPath
        read string f, GG_BGPath
        read float f, OPTION_SecsPerDay
        read float f, OPTION_AutoSkipSecs
        read float f, OPTION_FileIdleTime
        read file f, FLAG_MultiSampling
        read file f, FLAG_Fullscreen
        read file f, FLAG_Key
        read file f, FLAG_Loop
        read file f, FLAG_Realtime
        read file f, FLAG_NoVSync
        read long f, FLAG_Hide
        read long f, COLOR_Background
        read long f, COLOR_Font
    close file f
    set dir temp$
    Success = 1
endfunction Success
